# Files/Directories in this directory will be individually output as nixos modules
# Files can be marked as disabled by prefixing with "_" or suffixing with anything other than ".nix"
{ lib, ... }:
let
  # List module files in the current directory
  fileModules = lib.attrNames (
    lib.attrsets.filterAttrs (
      n: v:
      (
        n != "default.nix" # Exclude this file
        && v == "regular" # Don't include directories
        && (lib.strings.hasSuffix ".nix" n) # Must be a .nix file
        && !(lib.strings.hasPrefix "_" n) # Cannot start with "_"
      )
    ) (builtins.readDir ./.)
  );

  # Make modules for all files
  mkFileModules = lib.genAttrs' fileModules (
    n: lib.nameValuePair (lib.removeSuffix ".nix" n) { imports = [ ./${n} ]; }
  );

  # List module directories in the current directory
  dirModules = lib.attrNames (
    lib.attrsets.filterAttrs (n: v: v == "directory" && !(lib.strings.hasPrefix "_" n)) (
      builtins.readDir ./.
    )
  );

  # Turn list of directory names into paths
  mkDirImports =
    n:
    lib.filter (
      n: (lib.hasSuffix ".nix" n) && !(lib.hasPrefix "_" (lib.last (lib.splitString "/" n)))
    ) (lib.filesystem.listFilesRecursive ./${n});

  # Make modules for all directories
  mkDirModules = lib.genAttrs dirModules (n: {
    imports = mkDirImports n;
  });
in
{
  flake.nixosModules = mkFileModules // mkDirModules;
}
